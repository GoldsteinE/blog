+++
title = "Решаем задачи Advent of Code 2020 на GNU Assembler — часть 2"
date = 2021-03-29
path = "asm2"

[extra]
toc = true
+++

[В предыдущем выпуске](@/asm/r1.md) мы написали достаточное количество функций в нашей
«стандартной библиотеке», чтобы приступить к решению первой задачи. Она довольно тривиальная, так
что можно считать это просто «пробой пера», попыткой написать на GAS хоть что-то осмысленное.

<!-- more -->

### <!-- {{{ --> Первая часть {#first}

Если хотите, можете прочитать [полное условие][task], но краткая суть заключается в следующем:

> Дан список чисел, по одному числу на строку. Найти среди них два числа, сумма которых равняется
> 2020, и вывести их произведение.

Я не знаю более разумного решения этой задачи, чем просто в двух вложенных циклах пройтись по входным
данным. Оно имеет квадратичную сложность, но для заданного объёма входных данных (200 чисел) это не
имеет значения.

#### Читаем числа {#reading-numbers}

Чтобы пройтись по массиву в двух вложенных циклах, его нужно для начала прочитать с клавиатуры.
Возникает вопрос: куда, собственно, мы хотим сохранить массив? В обычном языке мы бы выделили под
него память на куче (с помощью `malloc()` в C, или через более высокоуровневый интерфейс вроде
`Vec<_>` в Rust).

Тут возникает проблема: у нас нет `malloc()`. У нас также нет `Vec<_>`, нет `list()`, и нет абсолютно
никакого способа выделить память на куче, кроме как с помощью системного вызова `mmap` или `brk`.
Я абсолютно точно не хочу руками дёргать `mmap` и `brk`, поэтому мы пойдём другим путём.

##### Стек {#stack}

Стек — это непрерывная область в памяти программы. На вершину стека указывает регистр `rsp`.
Адреса в стеке идут сверху вниз, то есть если мы хотим выделить 8 байт на стеке, мы пишем:

```gas
sub rsp, 8
```

Аналогично, освободить память со стека можно просто прибавив нужное количество байт к `rsp`.
Никаких системных вызовов, никакой сложности, никакой ОС-магии — это нам подходит.

Так как задача «выделить на стеке нужное количество байт и положить туда содержимое регистра»
встречается очень часто, для этого существует специальная инструкция `push`. Аналогично,
чтобы достать что-то со стека, есть инструкция `pop`.

```gas
# Вот эта инструкция
push rax
# примерно эквивалентна
sub rsp, 8
mov [rsp], rax
# Аналогично,
pop rax
# примерно то же самое, что
mov rax, [rsp]
add rsp, 8
```

Чтобы сохранить наш массив, мы просто последовательно за`push`им на стек все его числа. Из-за того,
что стек растёт вниз, массив получится перевёрнутым. Нам это неважно — порядок чисел в массиве не
играет никакой роли для нашей задачи.

##### Действительно читаем числа {#actually-reading-numbers}

Теперь мы знаём всё нужное, чтобы прочитать массив чисел. Чтобы не вставать два раза, заодно напишем
вывод ответа на экран, считая, что функция, которая находит ответ, уже существует.

```gas
.include "stdlib.s"

# Нам потребуется буфер для ввода/вывода
# Так как наши данные — 4-значные числа, 16 байт с лихвой хватит
.section .data
# .fill заполняет значением (по умолчанию 0) следующие 16 байт
buffer: .fill 16

.section .text

.global _start
_start:
    # Сохраним текущую вершину стека:
    # это будет указатель на конец массива
    mov r13, rsp

.L_start.input_loop:
    # Читаем строку с клавиатуры
    mov rdi, offset buffer
    call readline
    # Если мы не прочитали ни одного байта, выходим из цикла
    test rbx, rbx
    jz .L_start.input_loop_end
    # Запомним, нашли ли мы EOF
    mov r14, rax
    # Теперь нужно привести строку к числу
    mov rdi, offset buffer
    mov rsi, rbx
    call stoi
    # И сохранить на стек полученное значение
    push rax
    # Если мы ещё не нашли EOF, то повторяем
    test r14, r14
    jnz .L_start.input_loop
.L_start.input_loop_end:

    # Поместим указатели на начало и конец массива в аргументы
    mov rdi, rsp
    # Мы вычитаем размер одного «числа», чтобы указатель на конец
    # массива был валидным
    lea rsi, [r13 - 8]
    # И вызываем функцию, которая решит нам задачу
    call actually_solve_the_problem

    # Осталось перевести ответ в строку
    mov rdi, rax
    # Используем для этого тот же буфер, что и для чтения
    mov rsi, offset buffer
    call itos

    # И вывести ответ на экран
    mov rdi, offset buffer
    mov rsi, rax
    call putsn
    call putnl

    # Ура!
    exit 0
```

#### Ищем решение {#first-solution}

Алгоритм решения мы уже обсудили ранее. Единственное, что ещё нужно отметить — так как в наборе
чисел нет числа `1010`, нам не нужно волноваться о случае, когда и внешний, и внутренний цикл
смотрят на одно и то же число. В остальном всё легко:

```gas
# type: (int* start, int* end) -> int
.global find2_2020
find2_2020:
    # Сохраним указатель на начало массива в счётчик внешнего цикла
    mov rax, rdi
    # Нам стоит также сохранить где-нибудь число, которое мы ищем,
    # чтобы потом сравнивать с ним суммы
    mov r8, 2020
.Lfind2_2020.outer:
    # Скопируем счётчик внешнего цикла в счётчик внутреннего,
    # вместо того, чтобы ставить его в начало массива. Это позволит
    # нам не сравнивать пары по два раза.
    mov rbx, rax
.Lfind2_2020.inner:
    # Достаём два числа из массива
    mov rcx, [rax]
    mov rdx, [rbx]
    # Если их сумма равна 2020, то мы нашли ответ
    add rcx, rdx
    cmp rcx, r8
    je .Lfind2_2020.end
    # В противном случае, пробуем увеличить указатель из
    # внутреннего цикла
    add rbx, 8
    # Если мы ещё не дошли до конца массива, то повторяем
    # внутренний цикл
    cmp rbx, rsi
    jle .Lfind2_2020.inner
    # Если дошли, то пробуем увеличить указатель из
    # внешнего цикла
    add rax, 8
    cmp rax, rsi
    # Если в данных нет ошибки, то здесь мы ещё не дошли до конца,
    # и возвращаемся к началу внешнего цикла
    jle .Lfind2_2020.outer
    # Иначе мы знаем, что в данных нет нужной пары, и завершаемся
    # с ошибкой
    exit 255
.Lfind2_2020.end:
    # Мы уже потеряли значение за внешним указателем, прибавив
    # к нему `rdx`, так что восстановим его в регистре `rax`
    mov rax, [rax]
    # А вот значение за внутренним указателем всё ещё лежит в `rdx`
    # Достаточно просто домножить на него ответ
    imul rax, rdx
    # И вернуть пользователю
    ret
```

Если теперь подставить `find2_2020` на место `actually_solve_the_problem`, то можно увидеть...

```shell session
$ just runex 1
as "1/code.s" -o "1/obj.o"
ld "1/obj.o" -o "1/exe"
"1/exe" < "1/example.txt"
514579
$ just run 1
as "1/code.s" -o "1/obj.o"
ld "1/obj.o" -o "1/exe"
"1/exe" < "1/example.txt"
1007104
```

...что первая часть задачи успешно решена. Не так сложно, как казалось.

> Можно заметить, что Just ассемблирует и линкует наш код каждый раз. Дело в том, что, в отличие от
> Make, в Just нет возможности кешировать промежуточные результаты. Это могло бы быть проблемой,
> если бы «компиляция» ассемблера не выполнялась мгновенно.

<!-- }}} -->

### <!-- {{{ --> Вторая часть {#second}

Краткий пересказ условия:

> Дан список чисел, по одному числу на строку. Найти среди них три числа, сумма которых равняется
> 2020, и вывести их произведение.

Да, изменилось только то, что теперь нужно найти не два числа, а три. Соответственно, изменения
в функции, которая ищет ответ, тоже совершенно механические, и я на время снижу свои стандарты
комментирования кода. Если вам тоже очевидно, как нужно модифицировать `find2_2020`, чтобы получить
`find3_2020`, можете просто [пропустить](#chooseimpl) этот сниппет.

```gas
# type: (int* start, int* end) -> int
.global find3_2020
find3_2020:
    mov rax, rdi
    mov r8, 2020
.Lfind3_2020.outer:
    mov rbx, rax
.Lfind3_2020.middle:
    # Каждый указатель начинает с значения предыдущего
    mov rcx, rbx
.Lfind3_2020.inner:
    mov rdx, [rax]
    mov r9,  [rbx]
    # У нас кончились регистры, которые мы можем трогать, поэтому
    # сначала прибавим `[rbx]` к `[rax]`, а уже потом будем
    # вытаскивать значение из `[rcx]`.
    add rdx, r9
    mov r9,  [rcx]
    add rdx, r9
    cmp rdx, r8
    je .Lfind3_2020.end
    add rcx, 8
    cmp rcx, rsi
    jle .Lfind3_2020.inner
    add rbx, 8
    cmp rbx, rsi
    jle .Lfind3_2020.middle
    add rax, 8
    cmp rax, rsi
    jle .Lfind3_2020.outer
    exit 255
.Lfind3_2020.end:
    # Здесь сохранилось только значение `[rcx]`. `[rax]` и `[rbx]`
    # нужно снова доставать из памяти.
    mov rax, [rax]
    imul rax, r9
    mov r9,  [rbx]
    imul rax, r9
    ret
```

#### Выбираем нужную функцию {#chooseimpl}

Остаётся последняя задача: нужно при запуске программы выбрать, какую из двух функций мы хотим
вызвать. Для этого, в прошлой серии, мы написали макрос [chooseimpl](@/asm/r1.md#stdlib).

Теперь нам достаточно просто вызвать его в самом начале `_start`:

```gas
# ...
_start:
    # Положить в r15 указатель на нужную функцию
    chooseimpl r15, find2_2020, find3_2020
```

и после этого просто вызвать её через регистр:

```gas
call r15
```

Вот и всё, можно проверять:
```shell session
$ just runex2 1
...
241861950
$ just run2 1
...
18847752
```

<!-- }}} -->

### Заключение {#final-words}

Мы увидели, что в целом программирование на языке ассемблера во многом похоже на программирование
на C. Простые алгоритмы транслируются весьма тривиально, и, так как мы работаем с маленьким
количеством данных, мы не напоролись на проблемы с управлением памятью. Нашей «стандартной
библиотеки» пока что вполне хватает, чтобы писать простые программы, и всё идёт хорошо.

Чтобы разбавить скуку, повременим с решением второй задачи, и в следующей части займёмся более
пр*о*клятыми вещами.

[task]: https://adventofcode.com/2020/day/1
<!-- vim: set ts=4 sw=4 colorcolumn=100,68 et: -->
